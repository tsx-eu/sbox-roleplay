//=========================================================================================================================
// Optional
//=========================================================================================================================
HEADER
{
	CompileTargets = ( IS_SM_50 && ( PC || VULKAN ) );
	Description = "Shader for geometry generated by Voxels.VoxelMeshWriter";
}

//=========================================================================================================================
// Optional
//=========================================================================================================================
FEATURES
{
    #include "common/features.hlsl"
}

//=========================================================================================================================
COMMON
{
	#include "common/shared.hlsl"
}

//=========================================================================================================================

struct VertexInput
{
	#include "common/vertexinput.hlsl"
};

//=========================================================================================================================

struct PixelInput
{
	#include "common/pixelinput.hlsl"
};

//=========================================================================================================================

VS
{
	#include "common/vertex.hlsl"
	//
	// Main
	//
	PixelInput MainVs( VertexInput i )
	{
		PixelInput o = ProcessVertex( i );

		return FinalizeVertex( o );
	}
}

//=========================================================================================================================

PS
{
    #include "common/pixel.hlsl"

	CreateInputTexture2D( TopColor,            Srgb,   8, "",                 "_color",  "Top Material,10/10", Default3( 1.0, 1.0, 1.0 ) );
    CreateInputTexture2D( TopNormal,           Linear, 8, "NormalizeNormals", "_normal", "Top Material,10/20", Default3( 0.5, 0.5, 1.0 ) );
    CreateInputTexture2D( TopRoughness,        Linear, 8, "",                 "_rough",  "Top Material,10/30", Default( 0.5 ) );
	CreateInputTexture2D( TopMetalness,        Linear, 8, "",                 "_metal",  "Top Material,10/40", Default( 1.0 ) );
    CreateInputTexture2D( TopAmbientOcclusion, Linear, 8, "",                 "_ao",     "Top Material,10/50", Default( 1.0 ) );
    CreateInputTexture2D( TopMask,             Linear, 8, "",                 "_mask",   "Top Material,10/60", Default( 1.0 ) );

	CreateTexture2D( g_tTopColor )  < Channel( RGB,  Box( TopColor ), Srgb ); OutputFormat( BC7 ); SrgbRead( true ); >;
    CreateTexture2D( g_tTopNormal ) < Channel( RGBA, Box( TopNormal ), Linear ); OutputFormat( BC7 ); SrgbRead( false ); >;
    CreateTexture2D( g_tTopRma )    < Channel( R, Box( TopRoughness ), Linear ); Channel( G, Box( TopMetalness ), Linear ); Channel( B, Box( TopAmbientOcclusion ), Linear );  OutputFormat( BC7 ); SrgbRead( false ); >;
    CreateTexture2D( g_tTopMask )   < Channel( R, Box( TopMask ), Linear ); OutputFormat( BC7 ); SrgbRead( false ); >;

	//
	// Main
	//
	PixelOutput MainPs( PixelInput i )
	{
		float3 worldPos = (i.vPositionWithOffsetWs.xyz + g_vCameraPositionWs.xyz) / 64.0;

		float2 uvX = worldPos.yz * 1.0 + 0.0;
        float2 uvY = worldPos.xz * 1.0 + 0.0;
        float2 uvZ = worldPos.yx * 1.0 + 0.0;

		float3 triblend = saturate(pow(i.vNormalWs, 4));

		float topMask = saturate(pow(Tex2DS( g_tTopMask, TextureFiltering, uvZ ).x * i.vNormalWs.z, 4));

		triblend.z *= max(float(i.vNormalWs.z <= 0.0), topMask);

        triblend /= max(dot(triblend, half3(1,1,1)), 0.0001);

		float4 colX = Tex2DS( g_tColor, TextureFiltering, uvX );
        float4 colY = Tex2DS( g_tColor, TextureFiltering, uvY );
        float4 colZ = i.vNormalWs.z > 0.0 ? Tex2DS( g_tTopColor, TextureFiltering, uvZ ) : Tex2DS( g_tColor, TextureFiltering, uvZ );
        float4 col = colX * triblend.x + colY * triblend.y + colZ * triblend.z;

		float4 normX = Tex2DS( g_tNormal, TextureFiltering, uvX );
        float4 normY = Tex2DS( g_tNormal, TextureFiltering, uvY );
        float4 normZ = i.vNormalWs.z > 0.0 ? Tex2DS( g_tTopNormal, TextureFiltering, uvZ ) : Tex2DS( g_tNormal, TextureFiltering, uvZ );

		// I know this is wrong!
        float4 norm = normX * triblend.x + normY * triblend.y + normZ * triblend.z;
		
		float4 rmaX = Tex2DS( g_tRma, TextureFiltering, uvX );
        float4 rmaY = Tex2DS( g_tRma, TextureFiltering, uvY );
        float4 rmaZ = i.vNormalWs.z > 0.0 ? Tex2DS( g_tTopRma, TextureFiltering, uvZ ) : Tex2DS( g_tRma, TextureFiltering, uvZ );
        float4 rma = rmaX * triblend.x + rmaY * triblend.y + rmaZ * triblend.z;

		Material m = ToMaterial( col, norm, rma, g_flTintColor  );

		return FinalizePixelMaterial( i, m );
	}
}